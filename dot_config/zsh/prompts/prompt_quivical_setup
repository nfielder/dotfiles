# zsh prompt theme inspired by PaperColor theme <https://github.com/NLKNguyen/papercolor-theme>
# Written by nfielder

prompt_quivical_precmd() {
    vcs_info
    RPS1="${vcs_info_msg_0_}"
    if [[ -n $vcs_info_msg_1_ || -n $vcs_info_msg_2_ || -n $vcs_info_msg_3_ ]]; then
        RPS1+="%F{$colour15}("
        if [[ -n $vcs_info_msg_1_ ]]; then
            RPS1+="${vcs_info_msg_1_}"
        fi
        if [[ -n $vcs_info_msg_2_ ]]; then
            RPS1+="${vcs_info_msg_2_}"
        fi
        if [[ -n $vcs_info_msg_3_ ]]; then
            RPS1+="${vcs_info_msg_3_}"
        fi
        RPS1+="%F{$colour15})%f"
    fi
}

# vcs_info hooks
+vi-git-untracked(){
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
        git status --porcelain | grep -q '^?? ' 2> /dev/null ; then
        # This will show the marker if there are any untracked files in repo.
        hook_com[misc]="%F{$colour12}?%f"
    fi
}

prompt_quivical_setup () {
    # Vars
    local theme_mode=${1:-'dark'}
    local colour_mode=${PROMPT_QUIVICAL_COLOUR_MODE:-'24-bit'}

    # Colours
    case $colour_mode in
        24-bit)
            # Dark
            if [[ $theme_mode == 'dark' || $theme_mode != 'light' ]]; then
                colour0='#1c1c1c'
                colour1='#af005f'
                colour2='#5faf00'
                colour3='#d7af5f'
                colour4='#5fafd7'
                colour5='#808080'
                colour6='#d7875f'
                colour7='#d0d0d0'
                colour8='#585858'
                colour9='#5faf5f'
                colour10='#afd700'
                colour11='#af87d7'
                colour12='#ffaf00'
                colour13='#ff5faf'
                colour14='#00afaf'
                colour15='#5f8787'
            fi

            # Light
            if [[ $theme_mode == 'light' ]]; then
                colour0='#eeeeee'
                colour1='#af0000'
                colour2='#008700'
                colour3='#5f8700'
                colour4='#0087af'
                colour5='#878787'
                colour6='#005f87'
                colour7='#444444'
                colour8='#bcbcbc'
                colour9='#d70000'
                colour10='#d70087'
                colour11='#8700af'
                colour12='#d75f00'
                colour13='#d75f00'
                colour14='#005faf'
                colour15='#005f87'
            fi
            ;;
        8-bit)
            # Dark
            if [[ $theme_mode == 'dark' || $theme_mode != 'light' ]]; then
                colour0='234'
                colour1='125'
                colour2='70'
                colour3='179'
                colour4='74'
                colour5='244'
                colour6='173'
                colour7='252'
                colour8='240'
                colour9='71'
                colour10='148'
                colour11='140'
                colour12='214'
                colour13='205'
                colour14='37'
                colour15='66'
            fi

            # Light
            if [[ $theme_mode == 'light' ]]; then
                colour0='255'
                colour1='124'
                colour2='28'
                colour3='64'
                colour4='31'
                colour5='102'
                colour6='24'
                colour7='238'
                colour8='250'
                colour9='160'
                colour10='162'
                colour11='91'
                colour12='166'
                colour13='166'
                colour14='25'
                colour15='24'
            fi
            ;;
        *)
            # Default
            if [[ $theme_mode == 'dark' || $theme_mode != 'light' ]]; then
                colour0='0'
                colour1='1'
                colour2='2'
                colour3='3'
                colour4='4'
                colour5='5'
                colour6='6'
                colour7='7'
                colour8='8'
                colour9='9'
                colour10='10'
                colour11='11'
                colour12='12'
                colour13='13'
                colour14='14'
                colour15='15'
            fi

            # Light
            if [[ $theme_mode == 'light' ]]; then
                colour0='0'
                colour1='1'
                colour2='2'
                colour3='3'
                colour4='4'
                colour5='5'
                colour6='6'
                colour7='7'
                colour8='8'
                colour9='9'
                colour10='10'
                colour11='11'
                colour12='12'
                colour13='13'
                colour14='14'
                colour15='15'
            fi
            ;;
    esac

    # Determine if over SSH
    if [[ ! -z $SSH_CONNECTION ]]; then
        local ssh_prompt="%F{$colour5}[%F{$colour12}%m%F{$colour5}] "
    fi
        
    autoload -Uz vcs_info
    add-zsh-hook precmd prompt_quivical_precmd

    ## vcs_info Configuration ##
    # see https://zsh.sourceforge.io/Doc/Release/User-Contributions.html#Version-Control-Information
    zstyle ':vcs_info:*' enable git # Only use git backend
    zstyle ':vcs_info:*' max-exports 4
    zstyle ':vcs_info:git:*' check-for-changes true
    zstyle ':vcs_info:git:*' stagedstr "%F{$colour9}✚%f"
    zstyle ':vcs_info:git:*' unstagedstr "%F{$colour6}⦿%f"
    zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
    zstyle ':vcs_info:git:*' formats "%F{$colour5}[%F{$colour4}%b%F{$colour5}]%f" "%c" "%u" "%m"

    PS1="${ssh_prompt}%F{$colour4}%1~ %(2L.%F{$colour10}$SHLVL.)%(?.%F{$colour2}.%F{$colour1})%(#.#.❯)%f "
    PS2="%F{$colour3}> "

    prompt_opts=( cr percent sp )
}

prompt_quivical_setup "$@"
# vim: set filetype=zsh:
